#!/usr/bin/perl -w
#
# Copyright (c) 2000-2014 University of Utah and the Flux Group.
# 
# {{{EMULAB-LICENSE
# 
# This file is part of the Emulab network testbed software.
# 
# This file is free software: you can redistribute it and/or modify it
# under the terms of the GNU Affero General Public License as published by
# the Free Software Foundation, either version 3 of the License, or (at
# your option) any later version.
# 
# This file is distributed in the hope that it will be useful, but WITHOUT
# ANY WARRANTY; without even the implied warranty of MERCHANTABILITY or
# FITNESS FOR A PARTICULAR PURPOSE.  See the GNU Affero General Public
# License for more details.
# 
# You should have received a copy of the GNU Affero General Public License
# along with this file.  If not, see <http://www.gnu.org/licenses/>.
# 
# }}}
#
use English;
use strict;
use Getopt::Std;
use XML::Simple;
use Data::Dumper;
use CGI;
use POSIX ":sys_wait_h";
use POSIX qw(setsid);

#
# Back-end script to manage APT profiles.
#
sub usage()
{
    print("Usage: manage_profile [-u | -s uuid] <xmlfile>\n");
    print("Usage: manage_profile -r profile\n");
    exit(-1);
}
my $optlist     = "durs:t:";
my $debug       = 0;
my $verify      = 0;	# Check data and return status only.
my $update      = 0;
my $delete      = 0;
my $skipadmin   = 0;
my $snapuuid;
my $instance;
my $webtask;
my $webtask_id;

#
# Configure variables
#
my $TB		= "@prefix@";
my $TBOPS       = "@TBOPSEMAIL@";
my $QUICKVM     = "$TB/sbin/protogeni/quickvm";

#
# Untaint the path
#
$ENV{'PATH'} = "$TB/bin:$TB/sbin:/bin:/usr/bin:/usr/bin:/usr/sbin";
delete @ENV{'IFS', 'CDPATH', 'ENV', 'BASH_ENV'};

#
# Turn off line buffering on output
#
$| = 1;

#
# Load the Testbed support stuff.
#
use lib "@prefix@/lib";
use EmulabConstants;
use emdb;
use emutil;
use User;
use Project;
use APT_Profile;
use APT_Instance;
use GeniXML;
use GeniHRN;
use WebTask;
use libaudit;

# Protos
sub fatal($);
sub UserError();
sub DeleteProfile($);

#
# Parse command arguments. Once we return from getopts, all that should be
# left are the required arguments.
#
my %options = ();
if (! getopts($optlist, \%options)) {
    usage();
}
if (defined($options{"d"})) {
    $debug = 1;
}
if (defined($options{"v"})) {
    $verify = 1;
}
if (defined($options{"u"})) {
    $update = 1;
}
if (defined($options{"s"})) {
    $snapuuid = $options{"s"};
}
if (defined($options{"t"})) {
    $webtask_id = $options{"t"};
}
if (@ARGV != 1) {
    usage();
}
# The web interface (and in the future the xmlrpc interface) sets this.
my $this_user = User->ImpliedUser();
if (! defined($this_user)) {
    $this_user = User->ThisUser();
    if (!defined($this_user)) {
	fatal("You ($UID) do not exist!");
    }
}

# Remove profile.
if (defined($options{"r"})) {
    exit(DeleteProfile($ARGV[0]));
}
my $xmlfile  = shift(@ARGV);

#
# These are the fields that we allow to come in from the XMLfile.
#
my $SLOT_OPTIONAL	= 0x1;	# The field is not required.
my $SLOT_REQUIRED	= 0x2;  # The field is required and must be non-null.
my $SLOT_ADMINONLY	= 0x4;  # Only admins can set this field.
my $SLOT_UPDATE 	= 0x8;  # Allowed to update.
#
# XXX We should encode all of this in the DB so that we can generate the
# forms on the fly, as well as this checking code.
#
my %xmlfields =
    # XML Field Name        DB slot name         Flags             Default
    ("profile_name"	   => ["name",		$SLOT_REQUIRED],
     "profile_pid"	   => ["pid",		$SLOT_REQUIRED],
     "profile_creator"	   => ["creator",	$SLOT_OPTIONAL],
     "profile_listed"      => ["listed",	$SLOT_OPTIONAL|$SLOT_UPDATE],
     "profile_public"      => ["public",	$SLOT_OPTIONAL|$SLOT_UPDATE],
     "profile_shared"      => ["shared",	$SLOT_OPTIONAL|$SLOT_UPDATE],
     "rspec"		   => ["rspec",		$SLOT_REQUIRED|$SLOT_UPDATE],
);

#
# Must wrap the parser in eval since it exits on error.
#
my $xmlparse = eval { XMLin($xmlfile,
			    VarAttr => 'name',
			    ContentKey => '-content',
			    SuppressEmpty => undef); };
fatal($@)
    if ($@);

#
# Process and dump the errors (formatted for the web interface).
# We should probably XML format the errors instead but not sure I want
# to go there yet.
#
my %errors = ();

#
# Make sure all the required arguments were provided.
#
my $key;
foreach $key (keys(%xmlfields)) {
    my (undef, $required, undef) = @{$xmlfields{$key}};

    $errors{$key} = "Required value not provided"
	if ($required & $SLOT_REQUIRED  &&
	    ! exists($xmlparse->{'attribute'}->{"$key"}));
}
UserError()
    if (keys(%errors));

#
# We build up an array of arguments to create.
#
my %new_args = ();
my %update_args = ();

foreach $key (keys(%{ $xmlparse->{'attribute'} })) {
    my $value = $xmlparse->{'attribute'}->{"$key"}->{'value'};
    if (!defined($value)) {	# Empty string comes from XML as an undef value.
	$xmlparse->{'attribute'}->{"$key"}->{'value'} = $value = "";
    }

    print STDERR "User attribute: '$key' -> '$value'\n"
	if ($debug);

    my $field = $key;
    if (!exists($xmlfields{$field})) {
	next; # Skip it.
    }
    my ($dbslot, $required, $default) = @{$xmlfields{$field}};

    if ($required & $SLOT_REQUIRED) {
	# A slot that must be provided, so do not allow a null value.
	if (!defined($value)) {
	    $errors{$key} = "Must provide a non-null value";
	    next;
	}
    }
    if ($required & $SLOT_OPTIONAL) {
	# Optional slot. If value is null skip it. Might not be the correct
	# thing to do all the time?
	if (!defined($value)) {
	    next
		if (!defined($default));
	    $value = $default;
	}
    }
    if ($required & $SLOT_ADMINONLY && !$skipadmin) {
	# Admin implies optional, but thats probably not correct approach.
	$errors{$key} = "Administrators only"
	    if (! $this_user->IsAdmin());
    }
	
    # Now check that the value is legal.
    if (! TBcheck_dbslot($value, "apt_profiles",
			 $dbslot, TBDB_CHECKDBSLOT_ERROR)) {
	$errors{$key} = TBFieldErrorString();
	next;
    }
    $new_args{$dbslot} = $value;
    $update_args{$dbslot} = $value
	if ($update && ($required & $SLOT_UPDATE));
}
UserError()
    if (keys(%errors));

#
# We need to make sure the project exists and is a valid project for
# the creator (current user). 
#
my $project = Project->Lookup($new_args{"pid"});
if (!defined($project)) {
    $errors{"profile_pid"} = "No such project exists";
}
elsif (!$project->AccessCheck($this_user, TB_PROJECT_MAKEIMAGEID())) {
    $errors{"profile_pid"} = "Not enough permission in this project";
}

#
# Are we going to snapshot a node in an experiment? If so we
# sanity check to make sure there is just one node. 
#
if (defined($snapuuid)) {
    $instance = APT_Instance->Lookup($snapuuid);
    if (!defined($instance)) {
	fatal("Could not look up instance $snapuuid");
    }
    my $manifest = GeniXML::Parse($instance->manifest());
    if (! defined($manifest)) {
	fatal("Could not parse manifest");
    }
    my @nodes = GeniXML::FindNodes("n:node", $manifest)->get_nodelist();
    if (@nodes != 1) {
	$errors{"error"} = "Too many nodes (> 1) to snapshot";
	UserError();
    }
}

my $profile = APT_Profile->Lookup($new_args{"pid"}, $new_args{"name"});

if ($update) {
    if (!defined($profile)) {
	$errors{"profile_name"} = "No such profile exists";
	UserError();
    }
    # Kill the description.. No longer used.
    $update_args{"description"} = "";
    
    $profile->Update(\%update_args) == 0 or
	fatal("Could not update profile record");
    # Bump the modtime.
    $profile->MarkModified();
}
else {
    my $usererror;
    if (defined($profile)) {
	$errors{"profile_name"} = "Already in use";
	UserError();
    }
    $profile =
	APT_Profile->Create($project, $this_user, \%new_args, \$usererror);
    if (!defined($profile)) {
	if (defined($usererror)) {
	    $errors{"profile_name"} = $usererror;
	    UserError();
	}
	fatal("Could not create new profile");
    }
}

#
# Now do the snapshot operation.
#
if (defined($instance)) {
    my $manifest = GeniXML::Parse($instance->manifest());
    if (! defined($manifest)) {
	fatal("Could not parse manifest");
    }
    my ($node) = GeniXML::FindNodes("n:node", $manifest)->get_nodelist();
    my $sliver_urn = GeniXML::GetSliverId($node);
    my $apt_uuid   = $instance->uuid();
    my $imagename  = $profile->name();

    #
    # Grab the webtask object so we can watch it. We are looking
    # for it to finish, so we can unlock the profile for use. 
    #
    $webtask = WebTask->Create($profile->uuid(), $webtask_id);
    if (!defined($webtask)) {
	$profile->Delete();
    }
    $webtask->AutoStore(1);

    if ($profile->Lock()) {
	$profile->Delete();
	fatal("Could not lock new profile");
    }

    my $command = "$QUICKVM " . ($webtask_id ? "-t $webtask_id " : " ") .
	"-s $apt_uuid $sliver_urn $imagename";
    #
    # This returns pretty fast, and then the imaging takes place in
    # the background at the aggregate. quickvm keeps a process running
    # in the background waiting for the sliver to unlock and the
    # sliverstatus to indicate the node is running again. 
    #
    my $output = emutil::ExecQuiet($command);
    if ($?) {
	$profile->Delete();
	$webtask->Delete();
	fatal("Failed to create disk image!");
    }
    #
    # Parse the output to get the new image urn, then stick that
    # into the rspec, and update the database.
    #
    my $image_urn;
    if ($output =~ /^(urn:.*),/) {
	$image_urn = $1;
    }
    else {
	$profile->Delete();
	fatal("Could not find image urn in:\n$output");
    }
    my $rspec = GeniXML::Parse($profile->rspec());
    if (! defined($rspec)) {
	$profile->Delete();
	fatal("Could not parse rspec");
    }
    ($node) = GeniXML::FindNodes("n:node", $rspec)->get_nodelist();
    GeniXML::SetDiskImage($node, $image_urn);
    if ($profile->Update({"rspec" => GeniXML::Serialize($rspec)})) {
	$profile->Delete();
	fatal("Could not update rspec");
    }

    #
    # Exit and leave child to poll.
    #
    if (! $debug) {
	my $child = fork();
	if ($child) {
	    exit(0);
	}
	# Let parent exit;
	sleep(2);
	if (0) {
	    AuditStart(0, undef, LIBAUDIT_LOGTBLOGS()|LIBAUDIT_LOGONLY());
	    AddAuditInfo("cc", "aptnet-logs\@flux.utah.edu");
	}
	POSIX::setsid();
    }
    my $seconds  = 1200;
    my $interval = 5;

    while ($seconds >= 0) {
	sleep($interval);
	$seconds -= $interval;

	$webtask->Refresh();
	last
	    if (defined($webtask->exited()));
    }
    if ($webtask->exitcode()) {
	$profile->Delete();
	exit(1);
    }
    $profile->Unlock();
    exit(0);
}
exit(0);

sub fatal($)
{
    my ($mesg) = @_;

    print STDERR "*** $0:\n".
	         "    $mesg\n";
    # Exit with negative status so web interface treats it as system error.
    exit(-1);
}

#
# Generate a simple XML file that PHP can parse. The web interface
# relies on using the same name attributes for the errors, as for
# the incoming values.
#
sub UserError()
{
    if (keys(%errors)) {
	print "<errors>\n";
	foreach my $key (keys(%errors)) {
    	    print "<error name='$key'>" . CGI::escapeHTML($errors{$key});
	    print "</error>\n";
	}
	print "</errors>\n";
    }
    # Exit with positive status so web interface treats it as user error.
    exit(1);
}

sub escapeshellarg($)
{
    my ($str) = @_;

    $str =~ s/[^[:alnum:]]/\\$&/g;
    return $str;
}

#
# Delete a profile.
#
sub DeleteProfile($)
{
    my ($name)  = @_;
    my $profile = APT_Profile->Lookup($name);
    if (!defined($profile)) {
	fatal("No such profile exists");
    }
    $profile->Delete() == 0 or
	fatal("Could not delete profile");
    return 0;
}
