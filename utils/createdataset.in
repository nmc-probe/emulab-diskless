#!/usr/bin/perl -w
#
# Copyright (c) 2013 University of Utah and the Flux Group.
# 
# {{{EMULAB-LICENSE
# 
# This file is part of the Emulab network testbed software.
# 
# This file is free software: you can redistribute it and/or modify it
# under the terms of the GNU Affero General Public License as published by
# the Free Software Foundation, either version 3 of the License, or (at
# your option) any later version.
# 
# This file is distributed in the hope that it will be useful, but WITHOUT
# ANY WARRANTY; without even the implied warranty of MERCHANTABILITY or
# FITNESS FOR A PARTICULAR PURPOSE.  See the GNU Affero General Public
# License for more details.
# 
# You should have received a copy of the GNU Affero General Public License
# along with this file.  If not, see <http://www.gnu.org/licenses/>.
# 
# }}}
#
use strict;
use English;
use Getopt::Std;
use Date::Parse;

#
# Create a new dataset.
#
sub usage()
{
    print STDERR "Usage: createdataset [-hdU] [-o uid] [-a attrs] [-t type] [-e expiration] -s size name\n";
    print STDERR "   -h       This message\n";
    print STDERR "   -d       Print additional debug info\n";
    print STDERR "   -U       Create but do not approve; admin will need to approve later\n";
    print STDERR "   -s size  Size in MiB\n";
    print STDERR "   -t type  Type ('stdataset' or 'ltdataset')\n";
    print STDERR "   -o uid   Owner (defaults to caller)\n";
    print STDERR "   -e date  Expiration date (or 'never')\n";
    print STDERR "   -a attrs comma-seperated string of key=value attributes\n";
    print STDERR "   name     Name (in the form <pid>/<id>)\n";
    exit(-1);
}
my $optlist  = "dhUo:s:t:e:a:";
my $debug = 0;
my $pid;
my $uid;
my $expire;
my $dstype = "stdataset";
my $lname;
my $size;
my $approveme = 1;
my $attrstr = "";
my %attrs = ();

my $qname = "global_ltdataset";
my $quota;

# Valid dataset types
my %descrip = (
    "stdataset" => "short-term dataset",
    "ltdataset" => "long-term dataset"
);

# Protos
sub fatal($);

#
# Configure variables
#
my $TB		 = "@prefix@";

#
# Testbed Support libraries
#
use lib "@prefix@/lib";
use libdb;
use Quota;
use Lease;
use Project;
use User;

#
# Turn off line buffering on output
#
$| = 1;

#
# Untaint the path
# 
$ENV{'PATH'} = "/bin:/sbin:/usr/bin:";

#
# Parse command arguments. Once we return from getopts, all that should be
# left are the required arguments.
#
my %options = ();
if (! getopts($optlist, \%options)) {
    usage();
}
if (defined($options{h})) {
    usage();
}
if (defined($options{d})) {
    $debug++;
}
if (defined($options{U})) {
    $approveme = 0;
}
if (defined($options{o})) {
    $uid = $options{o};
}
if (defined($options{s})) {
    if ($options{s} =~ /^(\d+)$/) {
	$size = $1;
    } else {
	fatal("Could not parse size.");
    }
}
if (defined($options{t})) {
    $dstype = $options{t};
}
if (defined($options{e})) {
    if ($options{e} eq "never") {
	$expire = 0;
    } else {
	$expire = str2time($options{e});
	if (!defined($expire)) {
	    fatal("Could not parse expiration date.");
	}
    }
}
if (defined($options{a})) {
    $attrstr = $options{a};
}

if (!$size || @ARGV != 1) {
    print STDERR "Must specify size and name\n";
    usage();
}

# name must include a project
$lname = $ARGV[0];
if ($lname =~ /^([-\w]+)\/([-\w]+)$/) {
    $pid = $1;
    $lname = $2;
} else {
    fatal("Dataset name $lname not in the form <pid>/<lname>.");
}

my $this_user = User->ThisUser();
if (! defined($this_user)) {
    fatal("You ($UID) do not exist!");
}

#
# Check owner: caller must be admin or be the specified user.
#
my $user;
if ($uid) {
    $user = User->Lookup($uid);
    if (TBAdmin()) {
	if (!defined($user)) {
	    fatal("No such user $uid");
	}
    } else {
	if (!$user || !$user->SameUser($this_user)) {
	    fatal("Must be admin to act on behalf of uid $uid");
	}
    }
} else {
    $user = $this_user;
    $uid = $this_user->uid();
}

#
# Check project: caller must be admin or have local_root access in the project.
#
my $project = Project->Lookup($pid);
if (!defined($project)) {
    fatal("No such project $pid");
}
if (!TBAdmin() &&
    !$project->AccessCheck($this_user, TB_PROJECT_CREATELEASE())) {
    fatal("Must have local_root privileges in pid $pid");
}

#
# Check type: currently only two defined.
#
if (!exists($descrip{$dstype})) {
    print STDERR "Invalid dataset type $dstype, should be one of:\n";
    foreach my $l (keys %descrip) {
	print STDERR "'$l': ", $descrip{$l}, "\n";
    }
    exit(1);
}

#
# Fetch default values for the lease type. We use:
#
# maxsize	Max size (MiB) of a dataset
#		(0 == unlimited)
# maxlease	Max time (days) from creation before lease is marked expired
#		(0 == unlimited)
# usequotas	If non-zero, enforce per-project dataset quotas
#
my $vars = Lease->SiteVars($dstype);
# convert to seconds
$vars->{"maxlease"} *= (24 * 60 * 60);

#
# Check size: size must be > 0 and the size must be less than site limit
# (if there is one).
#
if ($size <= 0) {
    fatal("Size must be greater than zero.");
}
if ($approveme && $vars->{"maxsize"} > 0 && $size > $vars->{"maxsize"}) {
    print STDERR
	"Requested size ($size) is larger than allowed by default (" .
	$vars->{"maxsize"} . ").\n";
    print STDERR
	"Try again with '-U' option and request special approval by testbed-ops.\n";
    exit(1);
}

#
# Check expiration: must be in the future and within the site-specific
# max lengths. Note that an expire value of zero means "unlimited".
# If the user did not specify a value, we use the system max value.
#
my $now = time();
my $needapproval = 0;
if (!defined($expire)) {
    # no user specified expiration, use default max (which may be unlimited)
    if ($vars->{"maxlease"} > 0) {
	$expire = $now + $vars->{"maxlease"};
    } else {
	$expire = 0;
    }
} elsif ($expire == 0) {
    # user specified unlimited, requires approval unless max is also unlimited
    if ($vars->{"maxlease"} > 0) {
	if ($approveme) {
	    print STDERR "Unlimited expiration not allowed by default.\n";
	    $needapproval = 1;
	}
    }
} else {
    # user specified a date
    if ($expire < $now) {
	fatal("Expiration date is in the past!");
    }
    if ($approveme && ($expire - $now) > $vars->{"maxlease"}) {
	my $mdate = localtime($now + $vars->{"maxlease"});
	my $rdate = localtime($expire);
	print STDERR "Expiration is beyond the maximum allowed by default ".
	    "($rdate > $mdate).\n";
	$needapproval = 1;
    }
}
if ($needapproval) {
    print STDERR
	"Try again with '-U' to request special approval by testbed-ops.\n";
    exit(1);
}

#
# Check attributes: right now, must just be a well-formed string.
#
foreach my $kv (split(',', $attrstr)) {
    if ($kv =~ /^([-\w]+)=([-\w\.\+\/:]+)$/) {
	# XXX filter out any size= attribute, we will re-add at the end.
	if ($1 eq "size") {
	    next;
	}
	$attrs{$1} = $2;
    } else {
	fatal("Malformed attribute string '$attrstr'.");
    }
}
$attrs{'size'} = $size;

#
# Check name: lease with this name must not already exist.
#
if (Lease->Lookup($pid, $lname)) {
    fatal("Lease $pid/$lname already exists.");
}

#
# Check quota: if we are enforcing one, size + all existing leases
# must be below the project limit.
#
if ($vars->{"usequotas"}) {
    $quota = Quota->Lookup($pid, $qname);
    if (!defined($quota)) {
	fatal("No $qname quota associated with $pid.");
    }

    #
    # Lock the quota object while we are performing the check.
    #
    # This is the convention we used to atomically access quotas.
    # Blockstores that count against the quota should not come or go
    # while the lock is held, nor should the quota change.
    #
    if ($quota->Lock()) {
	fatal("Could not lock $pid quota object, try later.");
    }

    #
    # Unless they have an unlimited quota, add up all the existing
    # ltdataset datasets to see if the new one will put them over quota.
    #
    if (!$quota->IsUnlimited()) {
	my $qsize = $quota->size();
	my $allocated = 0;

	my @datasets = Lease->AllProjectLeases($pid, $dstype);
	foreach my $dataset (@datasets) {
	    my $lsize = $dataset->GetAttribute("size");
	    fatal("ltdataset dataset $dataset has no size!?")
		if (!defined($lsize));
	    $allocated += $lsize;
	}
	if ($allocated + $size > $qsize) {
	    fatal("Allocation of $size would put $pid over quota ($qsize).");
	}
    }
}

my $args = {
    "lease_id" => $lname,
    "pid" => $project,
    "uid" => $user,
    "type" => $dstype,
    "lease_end" => $expire,
    "state" => "unapproved"
};
my $lease = Lease->Create($args, \%attrs);
if (!$lease) {
    fatal("Could not create dataset lease $lname in $pid.");
}

# No longer need to hold the quota lock
if (defined($quota)) {
    $quota->Unlock();
    $quota = undef;
}

#
# Approve the lease unless otherwise told not to.
# This can take a long time so we lock it to avoid races with others.
#
if ($approveme) {
    if ($lease->Lock() || $lease->AllocResources("valid")) {
	print STDERR "WARNING: could not approve new lease, contact testbed-ops.\n";
	$approveme = 0;
    }
    $lease->Unlock()
	if ($lease->GotLock());
}

print "Created lease '$pid/$lname' for " . $descrip{$dstype};
if ($expire == 0) {
    print ", never expires.\n";
} else {
    print ", expires on " . localtime($expire) . ".\n";
}
if (!$approveme) {
    # send message to testbed-ops to approve?
    print "NOTE: lease must still be approved before it can be used\n";
}

exit(0);

sub fatal($)
{
    my ($mesg) = $_[0];

    $quota->Unlock()
	if (defined($quota));
    die("*** $0:\n".
	"    $mesg\n");
}

